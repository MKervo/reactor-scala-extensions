group 'com.github.sinwe'
description = 'Scala wrapper for reactor-core'

apply plugin: 'scala'
apply plugin: "com.github.maiflai.scalatest"
apply plugin: 'signing'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

ext {
    reactorVersion = "3.0.4.RELEASE"
    baseScalaVersion = "2.11"
}

dependencies {
// scala dependencies
    compile "org.scala-lang:scala-library:$baseScalaVersion.8"
// reactor dependencies
    compile group: 'io.projectreactor', name: 'reactor-core', version: "$reactorVersion"

//    test dependencies
    testCompile "org.scalatest:scalatest_$baseScalaVersion:3.0.1"
    testRuntime "org.pegdown:pegdown:1.6.0"
    testCompile group: 'io.projectreactor.addons', name: 'reactor-test', version: "$reactorVersion"
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.14"
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("publish")}
    sign configurations.archives
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

ext.sonatypeUsername = hasProperty("ossrhUsername") ? ossrhUsername : System.getenv("ossrhUsername")

ext.sonatypePassword = hasProperty("ossrhPassword") ? ossrhPassword : System.getenv("ossrhPassword")

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
        }
    }
}
